@Behaviour ElasticitySatohTest;
@Author Helfer Thomas;
@Date 23/11/06;
@Description{
 A very first test 
 (the most simple one in fact).
}

@UseQt true;

@MaterialProperty stress young;
young.setGlossaryName("YoungModulus");
@MaterialProperty real nu;
nu.setGlossaryName("PoissonRatio");

@Parameter thermalexpansion alpha = 1e-5;
@StaticVariable temperature Tref = 293.15;

@AuxiliaryStateVariable temperature ImposedTemperature;
@AuxiliaryStateVariable stress pr;
pr.setGlossaryName("HydrostaticPressure");

// Lame Coefficients
@LocalVariable stress lambda,mu;

@InitLocalVariables{
  lambda = computeLambda(young,nu);
  mu = computeMu(young,nu);
}

@Integrator{
  constexpr const auto id = Stensor::Id();
  const auto eth = alpha * (T + dT - Tref) * id;
  const auto e = eto + deto - eth;
  sig = lambda * trace(e) * id + 2 * mu * e;
}

@UpdateAuxiliaryStateVariables{
  ImposedTemperature = T + dT;
  pr = trace(sig) / 3;
}

@TangentOperator {
  Dt = lambda * Stensor4::IxI() + 2 * mu * Stensor4::Id();
}
